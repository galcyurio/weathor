buildscript {
    ext.config = [
            group  : "com.github.galcyurio.weathor",
            version: "0.1.1",
            user   : "galcyurio",
            website: "https://github.com/galcyurio/weathor"
    ]

    ext.versions = [
            kotlin        : "1.2.40",
            testSets      : "1.4.5",
            bintrayRelease: "0.8.1",

            jackson       : "2.9.5",
            okhttp        : "3.10.0",
            retrofit      : "2.4.0",

            junit         : "4.12",
            assertj       : "3.9.0"
    ]

    ext.deps = [
            kotlinStdLib            : "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${versions.kotlin}",

            jackson                 : "com.fasterxml.jackson.core:jackson-databind:${versions.jackson}",
            jacksonModuleKotlin     : "com.fasterxml.jackson.module:jackson-module-kotlin:${versions.jackson}",

            okhttp                  : "com.squareup.okhttp3:okhttp:${versions.okhttp}",
            retrofit                : "com.squareup.retrofit2:retrofit:${versions.retrofit}",
            retrofitJacksonConverter: "com.squareup.retrofit2:converter-jackson:${versions.retrofit}",

            junit                   : "junit:junit:${versions.junit}",
            assertj                 : "org.assertj:assertj-core:${versions.assertj}",
            okhttpMockWebServer     : "com.squareup.okhttp3:mockwebserver:${versions.okhttp}"
    ]

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "org.unbroken-dome.gradle-plugins:gradle-testsets-plugin:${versions.testSets}"
        classpath "com.novoda:bintray-release:${versions.bintrayRelease}"
    }
}

allprojects {
    group = config.group
    version = config.version
}

subprojects {
    apply plugin: 'kotlin'
    apply plugin: 'org.unbroken-dome.test-sets'
    apply plugin: 'idea'

    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        testImplementation deps.junit
        testImplementation deps.assertj
    }

    testSets {
        integrationTest
    }

    idea {
        module {
            testSourceDirs += file("src/integrationTest/kotlin")
            testSourceDirs += file("src/integrationTest/java")
        }
    }

    compileIntegrationTestJava.options.encoding = "UTF-8"
    integrationTest { outputs.upToDateWhen { false } }

    check.dependsOn integrationTest
    integrationTest.mustRunAfter test

    compileKotlin {
        kotlinOptions.jvmTarget = "1.6"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
}